import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id "acme.java-common-conventions"
  id "application"
  id "org.beryx.jlink"
}

jlink {
  options = [
    "--strip-native-commands", "" +
    "--strip-debug",
    "--no-man-pages",
    "--no-header-files",
    "--compress", "2",
    "--include-locales", "en,de",
    "--verbose",
  ]
  jpackage {
    imageOptions += [
      "--description", "Hello World is an example JavaFX app and template project.",
      "--vendor", "ACME Ltd.",
    ]
    installerOptions = [
      "--license-file", "../LICENSE.txt",
    ]
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = "src/main/mac/app-icon.icns"
      imageOptions += [
        "--copyright", "Copyright Â© ${copyrightYear} ACME Ltd.",
        "--mac-sign",
        "--mac-signing-key-user-name", "Falko Schumann (QC6EN37P56)",
        "--resource-dir", "$projectDir/src/main/mac",
      ]
      installerType = "dmg"
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = "src/main/win/app-icon.ico"
      imageOptions = [
        "--copyright", "Copyright (c) ${copyrightYear} ACME Ltd.",
        "--resource-dir", "$projectDir/src/main/win",
      ]
      installerType = "msi"
      installerOptions = [
        "--win-dir-chooser",
        "--win-menu",
        "--win-menu-group", "ACME Ltd.",
      ]
    }
  }
}
