import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id 'application'
  id 'org.beryx.jlink'
  id 'acme.java-common-conventions'
}

jlink {
  options = [
    //'--strip-native-commands',
    '--strip-debug',
    '--no-man-pages',
    '--no-header-files',
    '--compress', '2',
    '--include-locales', 'en,de',
  ]
  jpackage {
    imageOptions += [
      '--description', 'Hello World is an example JavaFX app and template project.',
      '--vendor', 'ACME Ltd.',
    ]
    installerOptions = [
      '--license-file', "$rootDir/LICENSE.txt",
    ]
  }
  mergedModule {
    excludeRequires 'java.sql'
  }
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/mac/app-icon.icns"
      imageOptions += [
        '--copyright', "Copyright Â© ${copyrightYear} ACME Ltd.",
        '--resource-dir', "$projectDir/src/main/mac",
        '--mac-sign',
        '--mac-signing-key-user-name', 'Falko Schumann (QC6EN37P56)',
      ]
      installerType = 'dmg'
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = "$projectDir/src/main/win/app-icon.ico"
      imageOptions = [
        '--copyright', "Copyright (c) ${copyrightYear} ACME Ltd.",
        '--resource-dir', "$projectDir/src/main/win",
      ]
      installerType = 'msi'
      installerOptions = [
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'ACME Ltd.',
      ]
    }
  }
}
